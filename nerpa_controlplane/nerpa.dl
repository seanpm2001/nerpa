/*
Copyright (c) 2018-2020 VMware, Inc.
SPDX-License-Identifier: MIT
Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:
The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.
THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
*/

typedef PortId = bit<16>        // Port number.
typedef VID = bit<12>           // VLAN Identifier (0-4095).
typedef PortConfig = Access{vlan: VID} | Trunks{vlans : Set<VID>}

// Configuration for an individual port
input relation Port(port_id: PortId, config: PortConfig)
    primary key (x) (x.port_id)

// Each row indicates that 'port_id' handles VLAN 'vlan'.
// A port can be associated with multiple VLANs (if it's a trunk port).
relation VlanMapping(port_id: PortId, vlan: VID)
VlanMapping(port_id, vlan) :- Port(port_id, Access{vlan}).
VlanMapping(port_id, vlan) :-
    Port(port_id, Trunks{vlans}),
    var vlan = FlatMap(vlans).

// Each row indicates that VLAN 'vlan' spans the given 'ports'.
output relation VlanPorts(vlan: VID, ports: Set<PortId>)
VlanPorts(vlan, ports) :-
    VlanMapping(port_id, vlan),
    var ports = port_id.group_by(vlan).to_set().
